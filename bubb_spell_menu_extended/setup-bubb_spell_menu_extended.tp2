BACKUP ~bubb_spell_menu_extended/backup~
AUTHOR ~Bubb~
VERSION 2.2

BEGIN ~Bubb's Spell Menu Extended~
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~UI.MENU~ ~UI.MENU not found in game.~
REQUIRE_PREDICATE (
	   (GAME_IS ~bg2ee~)
	OR (GAME_IS ~bgee~ AND GAME_INCLUDES ~sod~) 
	OR (GAME_IS ~eet~)
	OR (GAME_IS ~iwdee~) 
) ~Game not supported. If you are trying to install on bgee, did you first run modmerge?~

	INCLUDE ~bubb_spell_menu_extended/bubb_lib.tph~

	ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~WIN32~ BEGIN
	
		ACTION_DEFINE_ASSOCIATIVE_ARRAY accepted_responses BEGIN ~1~ => 1 ~2~ => 1 END
		LAF PROMPT_USER
		STR_VAR
			to_prompt = EVAL ~This installer can attempt to patch UI.MENU instead of overriding it on Windows platforms; which installation method do you wish to perform?%WNL% 1] Patch%WNL% 2] Override (Warning: Will either wipe or break any currently installed UI mods)~
			accepted_responses = ~accepted_responses~
		RET
			response
		END
	
	END ELSE BEGIN
	
		ACTION_DEFINE_ASSOCIATIVE_ARRAY accepted_responses BEGIN ~2~ => 1 END
		LAF PROMPT_USER
		STR_VAR
			to_prompt = EVAL ~This installer can attempt to patch UI.MENU instead of overriding it on Windows platforms; which installation method do you wish to perform?%WNL% 1] Patch (Disabled)%WNL% 2] Override (Warning: Will either wipe or break any currently installed UI mods)~
			accepted_responses = ~accepted_responses~
		RET
			response
		END
	
	END
	
	// Set base-specific variables
	ACTION_IF MOD_IS_INSTALLED ~dragonspear_ui++.tp2~ ~1~ BEGIN
	
		OUTER_SPRINT environment_directory ~bubb_spell_menu_extended\environments\Dragonspear UI++~
		OUTER_SPRINT bam_directory ~bubb_spell_menu_extended\environments\bgee+sod\bam~
	
	END ELSE ACTION_IF MOD_IS_INSTALLED ~LeUI.tp2~ ~0~ BEGIN
	
		OUTER_SPRINT environment_directory ~bubb_spell_menu_extended\environments\LeUI~
		OUTER_SPRINT bam_directory ~bubb_spell_menu_extended\environments\bg2ee\bam~
		
	END ELSE ACTION_IF MOD_IS_INSTALLED ~LeUI-IWDEE.tp2~ ~0~ BEGIN
	
		OUTER_SPRINT environment_directory ~bubb_spell_menu_extended\environments\LeUI-IWDEE~
		OUTER_SPRINT bam_directory ~bubb_spell_menu_extended\environments\iwdee\bam~
		
	END ELSE ACTION_IF MOD_IS_INSTALLED ~LeUI-SoD.tp2~ ~0~ BEGIN
	
		OUTER_SPRINT environment_directory ~bubb_spell_menu_extended\environments\LeUI-SoD~
		OUTER_SPRINT bam_directory ~bubb_spell_menu_extended\environments\bgee+sod\bam~
		
	END ELSE ACTION_IF GAME_IS ~bg2ee~ BEGIN
	
		OUTER_SPRINT environment_directory ~bubb_spell_menu_extended\environments\bg2ee~
		OUTER_SPRINT bam_directory ~bubb_spell_menu_extended\environments\bg2ee\bam~
		
	END ELSE ACTION_IF GAME_IS ~bgee~ AND GAME_INCLUDES ~sod~ BEGIN
	
		OUTER_SPRINT environment_directory ~bubb_spell_menu_extended\environments\bgee+sod~
		OUTER_SPRINT bam_directory ~bubb_spell_menu_extended\environments\bgee+sod\bam~
		
	END ELSE ACTION_IF GAME_IS ~eet~ BEGIN
	
		ACTION_IF NOT MOD_IS_INSTALLED ~EET_gui.tp2~ ~0~ BEGIN
		
			OUTER_SPRINT environment_directory ~bubb_spell_menu_extended\environments\eet~
			OUTER_SPRINT bam_directory ~bubb_spell_menu_extended\environments\bg2ee\bam~
			
		END ELSE BEGIN
		
			OUTER_SPRINT environment_directory ~bubb_spell_menu_extended\environments\eet-SoD_GUI~
			OUTER_SPRINT bam_directory ~bubb_spell_menu_extended\environments\bgee+sod\bam~
			
		END
		
	END ELSE ACTION_IF GAME_IS ~iwdee~ BEGIN
	
		OUTER_SPRINT environment_directory ~bubb_spell_menu_extended\environments\iwdee~
		OUTER_SPRINT bam_directory ~bubb_spell_menu_extended\environments\iwdee\bam~
		
	END ELSE BEGIN
	
		FAIL ~UI.MENU base not supported.~
		
	END
	
	ACTION_IF %response% == 1 BEGIN
	
		// Patch UI.MENU
		
		AT_NOW ~bubb_spell_menu_extended\work\UIUtil.exe cleanup~
		
		// Base Patches; for vanilla game and UI overhauls
		LAF B3_COPY_UIPATCH STR_VAR uipatch = ~0base~ END 
		
		// Tweak Patches
		ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2900~ BEGIN LAF B3_COPY_UIPATCH STR_VAR uipatch = ~1lefreut's Improved Record Screen~ END END
				
		COPY_EXISTING ~UI.MENU~ ~bubb_spell_menu_extended\work\to_patch.MENU~ IF_EXISTS
		AT_NOW ~bubb_spell_menu_extended\work\UIUtil.exe applypatches to_patch.MENU out.MENU~
		
		ACTION_IF FILE_EXISTS ~bubb_spell_menu_extended\work\failed.mrk~ BEGIN
			FAIL ~Could not patch UI.MENU.~
		END
		
		COPY ~bubb_spell_menu_extended\work\out.MENU~ ~override\UI.MENU~
		
		AT_NOW ~bubb_spell_menu_extended\work\UIUtil.exe cleanup~
		
	END ELSE BEGIN
	
		COPY ~%environment_directory%\copy\UI.MENU~ ~override~
	
	END
	
	COPY ~%bam_directory%~ ~override~

	COPY ~bubb_spell_menu_extended/copy/blank.txt~ ~override/M_B3SpIn.lua~
	OUTER_SPRINT append_string ~~
	
	OUTER_SET spell_count = 0
	OUTER_SPRINT append_string ~%append_string%B3spellInfo =~
	OUTER_SPRINT append_string ~%append_string%%WNL%{~
	
	COPY_EXISTING_REGEXP ~^.+\.SPL$~ ~override~

		LPF TO_HEX_NUMBER INT_VAR value = spell_count minDigits = 3 RET hex_uuid = hexNumber END

		READ_LONG 0x0008 unidentified_name_strref

		PATCH_IF unidentified_name_strref != 0 AND unidentified_name_strref != (0 - 1) BEGIN

			GET_STRREF unidentified_name_strref unidentified_name

			LPF ARRAY_GET_VALUE INT_VAR key = unidentified_name_strref STR_VAR array_name = ~processed_strrefs~ RET already_processed = exists original_value = value END

			PATCH_IF (already_processed == 0) BEGIN

				TEXT_SPRINT unidentified_name_altered ~SPELL_UUID:%hex_uuid%~
				SET inserted_strref = RESOLVE_STR_REF (~%unidentified_name_altered%~)
				WRITE_LONG 0x0008 inserted_strref

				READ_SHORT 0x001c spell_type
				READ_LONG 0x0034 spell_level
				
				// Get Spell Icon
				
				READ_SHORT 0x0068 extended_header_count
				PATCH_IF extended_header_count > 0 BEGIN
					READ_LONG 0x0064 extended_header_offset
					SET memorized_icon_offset = extended_header_offset + 0x0004
					READ_ASCII %memorized_icon_offset% spell_icon
				END ELSE BEGIN
					READ_ASCII 0x003a spell_icon
				END
				
				INNER_ACTION BEGIN

					LAF STRING_REPLACE STR_VAR string = EVAL ~%unidentified_name%~ to_replace = ~\~ with = ~\\~ RET unidentified_name_escaped = replaced_string END
					LAF STRING_REPLACE STR_VAR string = EVAL ~%unidentified_name_escaped%~ to_replace = EVAL ~%LNL%~ with = EVAL ~\%LNL%~ RET unidentified_name_escaped = replaced_string END
					LAF STRING_REPLACE STR_VAR string = EVAL ~%unidentified_name_escaped%~ to_replace = ~'~ with = ~\'~ RET unidentified_name_escaped = replaced_string END
					
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%['%hex_uuid%'] =~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%{~

					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['name'] = '%unidentified_name_escaped%',~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['original_name_strref'] = %unidentified_name_strref%,~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['resref'] = '%SOURCE_RES%',~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['spellType'] = %spell_type%,~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['spellLevel'] = %spell_level%,~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['spellIcon'] = '%spell_icon%',~

					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%},~
				END

				TEXT_SPRINT $processed_strrefs(~%unidentified_name_strref%~) ~%unidentified_name%~

			END ELSE BEGIN

				TEXT_SPRINT unidentified_name_altered ~SPELL_UUID:%hex_uuid%~
				SET inserted_strref = RESOLVE_STR_REF (~%unidentified_name_altered%~)
				WRITE_LONG 0x0008 inserted_strref

				READ_SHORT 0x001c spell_type
				READ_LONG 0x0034 spell_level
				
				// Get Spell Icon
				
				READ_SHORT 0x0068 extended_header_count
				PATCH_IF extended_header_count > 0 BEGIN
					READ_LONG 0x0064 extended_header_offset
					SET memorized_icon_offset = extended_header_offset + 0x0004
					READ_ASCII %memorized_icon_offset% spell_icon
				END ELSE BEGIN
					READ_ASCII 0x003a spell_icon
				END
				
				INNER_ACTION BEGIN

					LAF STRING_REPLACE STR_VAR string = EVAL ~%original_value%~ to_replace = ~\~ with = ~\\~ RET original_value_escaped = replaced_string END
					LAF STRING_REPLACE STR_VAR string = EVAL ~%original_value_escaped%~ to_replace = EVAL ~%LNL%~ with = EVAL ~\%LNL%~ RET original_value_escaped = replaced_string END
					LAF STRING_REPLACE STR_VAR string = EVAL ~%original_value_escaped%~ to_replace = ~'~ with = ~\'~ RET original_value_escaped = replaced_string END
					
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%['%hex_uuid%'] =~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%{~

					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['name'] = '%original_value_escaped%',~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['original_name_strref'] = %unidentified_name_strref%,~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['resref'] = '%SOURCE_RES%',~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['spellType'] = %spell_type%,~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['spellLevel'] = %spell_level%,~
					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['spellIcon'] = '%spell_icon%',~

					OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%},~
				END

				TEXT_SPRINT $processed_strrefs(~%inserted_strref%~) ~%original_value%~
			END
		END

		SET %spell_count% += 1

	BUT_ONLY_IF_IT_CHANGES

	ACTION_GET_STRREF 11555 identify_string
	ACTION_GET_STRREF 63205 jump_to_cleric
	ACTION_GET_STRREF 63206 jump_to_mage

	LAF STRING_REPLACE STR_VAR string = EVAL ~%identify_string%~ to_replace = ~'~ with = ~\'~ RET identify_string = replaced_string END
	LAF STRING_REPLACE STR_VAR string = EVAL ~%jump_to_cleric%~ to_replace = ~'~ with = ~\'~ RET jump_to_cleric = replaced_string END
	LAF STRING_REPLACE STR_VAR string = EVAL ~%jump_to_mage%~ to_replace = ~'~ with = ~\'~ RET jump_to_mage = replaced_string END

	OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%["specialTooltips"] =~
	OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%{~
	OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['%identify_string%'] = 1,~
	OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['%jump_to_cleric%'] = 1,~
	OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%%TAB%['%jump_to_mage%'] = 1,~
	OUTER_SPRINT append_string ~%append_string%%WNL%%TAB%},~

	OUTER_SPRINT append_string ~%append_string%%WNL%}~

    APPEND ~M_B3SpIn.lua~ ~%append_string%~
    OUTER_SPRINT append_string ~~
	